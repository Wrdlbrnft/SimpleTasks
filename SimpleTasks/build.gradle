apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation 'android.arch.lifecycle:extensions:1.1.0'
    implementation 'android.arch.lifecycle:viewmodel:1.1.0'
    implementation 'android.arch.lifecycle:livedata:1.1.0'
    implementation 'android.arch.lifecycle:common-java8:1.1.0'
}

final bintrayUser = System.getenv('BINTRAY_USER') ?: ''
final bintrayApiKey = System.getenv('BINTRAY_API_KEY') ?: ''
final versionName = "0.3.0.${System.getenv('TRAVIS_BUILD_NUMBER') ?: 0}"

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task javadoc(type: Javadoc, dependsOn: 'assembleRelease') {
    source = android.sourceSets.main.java.srcDirs
    title = 'SimpleTasks'
    options.links('http://docs.oracle.com/javase/7/docs/api/');
    options.linksOffline('http://d.android.com/reference', "${android.sdkDirectory}/docs/reference");
    classpath += files(android.libraryVariants.javaCompile.classpath.files)
    classpath += files(android.getBootClasspath())
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompile.classpath.files
    })
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        Bintray(MavenPublication) {
            groupId 'com.github.wrdlbrnft'
            artifactId 'simple-tasks'
            version versionName
            artifact sourcesJar
            artifact javadocJar
            artifact new File(buildDir, '/outputs/aar/SimpleTasks-release.aar')
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['Bintray']
    pkg {
        repo = 'maven'
        name = 'SimpleTasks'
        userOrg = bintrayUser
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Wrdlbrnft/SimpleTasks'
        publicDownloadNumbers = true
        version {
            name = versionName
            released = new Date()
        }
    }
}